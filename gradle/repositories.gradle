// This is a workaround for Gradle 5 that does not support PasswordCredentials.
// Once all apps are on Gradle 6+, we can remove this all and leave only
// the "repositories" block, using the method of described here:
// https://docs.gradle.org/6.8.1/userguide/declaring_repositories.html#sec:handling_credentials
def homePath = System.properties['user.home']
def props = new Properties()
file(homePath + "/.gradle/gradle.properties").withInputStream { props.load(it) }

//Releases and Experimental credentials are the same, so we choose Releases
def artifactsUser = props['AndroidInternalReleasesUsername']
def artifactsPass = props['AndroidInternalReleasesPassword']

repositories {
    // Meli internal release libs
    maven {
        name 'AndroidInternalReleases'
        url 'https://android.artifacts.furycloud.io/repository/releases/'
        credentials {
            username artifactsUser
            password artifactsPass
        }
        content {
            // only releases
            includeVersionByRegex('com\\.mercadolibre\\..*', '.*', '^((?!EXPERIMENTAL-|LOCAL-).)*$')
            includeVersionByRegex('com\\.mercadopago\\..*', '.*', '^((?!EXPERIMENTAL-|LOCAL-).)*$')
            includeVersionByRegex('com\\.mercadoenvios\\..*', '.*', '^((?!EXPERIMENTAL-|LOCAL-).)*$')
        }
    }

    // only used for experimental libs
    maven {
        name 'AndroidInternalExperimental'
        url 'https://android.artifacts.furycloud.io/repository/experimental'
        credentials {
            username artifactsUser
            password artifactsPass
        }
        content {
            includeVersionByRegex('com\\.mercadolibre\\.android.*', '.*', '^(.*-)?EXPERIMENTAL-.*$')
            includeVersionByRegex('com\\.mercadopago\\.android.*', '.*', '^(.*-)?EXPERIMENTAL-.*$')
            includeVersionByRegex('com\\.mercadoenvios\\.android.*', '.*', '^(.*-)?EXPERIMENTAL-.*$')
        }
    }

    mavenLocal {
        content {
            includeVersionByRegex('com\\.mercadolibre\\.android.*', '.*', '^(.*-)?LOCAL-.*$')
            includeVersionByRegex('com\\.mercadopago\\.android.*', '.*', '^(.*-)?LOCAL-.*$')
            includeVersionByRegex('com\\.mercadoenvios\\.android.*', '.*', '^(.*-)?LOCAL-.*$')
        }
    }

    maven {
        name 'AndroidGradlePlugins'
        url 'https://android.artifacts.furycloud.io/repository/gradle-plugins'
        credentials {
            username artifactsUser
            password artifactsPass
        }
    }
}

